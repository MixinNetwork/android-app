def versionMajor = 0
def versionMinor = 18
def versionPatch = 9
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "one.mixin.messenger"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.expandProjection":"true"]
            }
        }

        buildConfigField 'String', 'BUGSNAG_API_KEY', "\"${propOrEmpty('MIXIN_BUGSNAG_API_KEY')}\""
        buildConfigField 'String', 'RECAPTCHA_KEY', "\"${propOrEmpty('MIXIN_RECAPTCHA_KEY')}\""
        buildConfigField 'String', 'GIPHY_KEY', "\"${propOrEmpty('MIXIN_GIPHY_KEY')}\""
        buildConfigField 'String', 'SafetyNet_API_KEY', "\"${propOrEmpty('MIXIN_SAFETYNET_API_KEY')}\""

        packagingOptions {
            exclude 'kotlin/**'
            exclude '**/*.kotlin_metadata'
            exclude 'META-INF/*.kotlin_module'
            exclude 'META-INF/*.version'
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }

        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    productFlavors {
    }

    signingConfigs {
        debug {
            storeFile file('keys/debug.keystore')
            storePassword 'android'
            keyAlias 'AndroidDebugKey'
            keyPassword 'android'
        }
        if (project.hasProperty("RELEASE_STORE_FILE")) {
            release {
                keyAlias "${RELEASE_KEY_ALIAS}"
                keyPassword "${RELEASE_KEY_PASSWORD}"
                storeFile file("${RELEASE_STORE_FILE}")
                storePassword "${RELEASE_STORE_PASSWORD}"
            }
        }
    }

    buildTypes {
        if (project.hasProperty("RELEASE_STORE_FILE")) {
            release {
                ndk {
                    abiFilters "armeabi-v7a", "arm64-v8a"
                }
                debuggable false
                jniDebuggable false
                shrinkResources true
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
        debug {
            signingConfig signingConfigs.debug
            jniDebuggable false
            ndk {
                abiFilters "x86", "arm64-v8a"
            }
        }
    }

    lintOptions {
        abortOnError false
    }
    configurations {
        ktlint
    }

    configurations.all {
        resolutionStrategy.force "com.android.tools.build.jetifier:jetifier-core:1.0.0-beta08"
    }
}

dependencies {
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    implementation "androidx.fragment:fragment-ktx:${fragmentVersion}"
    implementation "androidx.appcompat:appcompat:${appcompatVersion}"
    implementation "androidx.legacy:legacy-support-v4:${supportLibVersion}"
    implementation "com.google.android.material:material:${supportLibVersion}"
    implementation "androidx.recyclerview:recyclerview:${supportLibVersion}"
    implementation "androidx.exifinterface:exifinterface:${exifinterfaceVersion}"
    implementation "androidx.browser:browser:${supportLibVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${constraintLayoutVersion}"
    implementation "androidx.core:core-ktx:${ktxVersion}"
    implementation "androidx.collection:collection:${collectionx}"
    implementation "androidx.collection:collection-ktx:${collectionx}"
    implementation "androidx.preference:preference-ktx:${preference_version}"
    implementation "androidx.viewpager2:viewpager2:${viewpagerVersion}"

    implementation "androidx.camera:camera-core:${cameraxVersion}"
    implementation "androidx.camera:camera-camera2:${cameraxVersion}"

    implementation "androidx.work:work-runtime-ktx:${workManagerVersion}"
    implementation "androidx.navigation:navigation-fragment-ktx:${navigationVersion}"
    implementation "androidx.navigation:navigation-ui-ktx:${navigationVersion}"

    // Architecture components
    implementation "androidx.paging:paging-runtime:${pagingVersion}"
    implementation "androidx.lifecycle:lifecycle-extensions:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${lifecycleVersion}"
    implementation "androidx.room:room-runtime:${roomVersion}"
    kapt "androidx.room:room-compiler:${roomVersion}"
    implementation "androidx.room:room-rxjava2:${roomVersion}"
    implementation "androidx.room:room-ktx:${roomVersion}"
    androidTestImplementation "android.arch.persistence.room:testing:${roomVersion}"
    // DI
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "com.google.dagger:dagger-android:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"
    kapt "com.google.dagger:dagger-android-processor:${daggerVersion}"
    kapt "com.squareup.inject:assisted-inject-processor-dagger2:${assistedinjectVersion}"
    compileOnly "com.squareup.inject:assisted-inject-annotations-dagger2:${assistedinjectVersion}"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
    implementation("io.jsonwebtoken:jjwt:${jwtVersion}")
    implementation "com.jakewharton.threetenabp:threetenabp:${threetenabpVersion}"
    implementation("com.bugsnag:bugsnag-android:${bugsnagVersion}") {
        exclude group: "com.bugsnag", module: "bugsnag-plugin-android-anr"
        exclude group: "com.bugsnag", module: "bugsnag-plugin-android-ndk"
    }
    implementation "org.jsoup:jsoup:${jsoupVersion}"

    // retrofit
    implementation "com.github.seniorzhai.okhttp:okhttp:${okhttpVersion}"
    implementation("com.squareup.retrofit2:retrofit:${retrofitVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation("com.squareup.retrofit2:converter-gson:${retrofitVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation("com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation("com.squareup.okhttp3:logging-interceptor:${okhttpVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation "com.jakewharton.rxbinding3:rxbinding:${rxbindingVersion}"
    implementation("com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:${coroutineAdapterVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }

    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.google.firebase:firebase-core:${firebaseVersion}"
    implementation "com.google.firebase:firebase-messaging:${firebaseMessageVersion}"
    implementation "com.google.firebase:firebase-dynamic-links:${firebaseDylinkVersion}"
    implementation "com.google.firebase:firebase-ml-vision:${firebaseMLVersion}"
    implementation("com.google.firebase:firebase-perf:${perfVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        exclude group: 'com.google.protobuf', module: 'protobuf-lite'
    }
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation "com.google.android.play:core:${playVersion}"
    implementation "com.google.android.gms:play-services-safetynet:${google_play_services_version}"
    implementation "com.google.zxing:core:${zxingVersion}"
    implementation "com.github.tougee:sticky-headers-recyclerview:${stickyheadersrecyclerviewVersion}"
    implementation "org.whispersystems:signal-protocol-android:${signalVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk25:$anko_version"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "com.zlc.glide:webpdecoder:$webpdecoderVesion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation("com.github.bumptech.glide:okhttp3-integration:$glideVersion") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "jp.wasabeef:glide-transformations:${glideTransformationsVersion}"
    implementation "com.jakewharton.timber:timber:${timberVersion}"
    implementation "com.facebook.stetho:stetho:${stethoVersion}"
    implementation("com.facebook.stetho:stetho-okhttp3:${stethoVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation "com.github.jorgecastilloprz:fabprogresscircle:${fabprogresscircleVersion}"
    implementation "com.github.tougee:country-picker-android:${countryPickerVersion}"
    implementation "com.dmitrymalkovich.android:progress-fab:${fabVersion}"
    implementation "com.googlecode.libphonenumber:libphonenumber:${libphonenumberVersion}"
    implementation "com.github.tougee:rx-contacts2:${rxContactsVersion}"
    implementation "com.github.tougee:android-priority-jobqueue:${jobqueueVersion}"
    implementation "com.github.tougee:rxpermissions:${rxpermissionsVersion}"
    implementation("com.github.yalantis:ucrop:${ucropVersion}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation "com.facebook.rebound:rebound:${reboundVersion}"
    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    ktlint "com.pinterest:ktlint:${ktlintVersion}"
    testImplementation "junit:junit:${junitVersion}"
    androidTestImplementation "junit:junit:${junitVersion}"
    androidTestImplementation("androidx.test.espresso:espresso-core:${espressoVersion}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "androidx.test.ext:junit:${androidxJunitVersion}"

    implementation "com.uber.autodispose:autodispose:${autodisposeVersion}"
    implementation "com.uber.autodispose:autodispose-android:${autodisposeVersion}"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:${autodisposeVersion}"
    implementation "com.uber.autodispose:autodispose-lifecycle:${autodisposeVersion}"
    implementation "com.google.android.exoplayer:exoplayer-core:${exoplayerVersion}"
    implementation "com.google.android.exoplayer:exoplayer-hls:${exoplayerVersion}"
    implementation "com.google.android.exoplayer:exoplayer-ui:${exoplayerVersion}"
    implementation "com.googlecode.mp4parser:isoparser:${isoparserVersion}"
    implementation "com.github.SeniorZhai:LargeImage:${largeImageViewVersion}"
    implementation "com.github.tougee:BiometricPromptCompat:${biometricpromptVersion}"
    implementation "io.noties.markwon:core:${markwonVersion}"
    implementation "io.noties.markwon:image:${markwonVersion}"
    implementation "io.noties.markwon:image-glide:${markwonVersion}"
    implementation "io.noties.markwon:ext-tables:${markwonVersion}"
    implementation "io.noties.markwon:recycler:${markwonVersion}"
    implementation "io.noties.markwon:recycler-table:${markwonVersion}"
    implementation "io.noties.markwon:ext-strikethrough:${markwonVersion}"
    implementation "io.noties:prism4j:${prism4jVersion}"
    implementation "io.noties.markwon:syntax-highlight:${markwonVersion}"
    implementation "org.webrtc:google-webrtc:${webrtcVersion}"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

configurations.all {
    exclude group: 'org.jetbrains', module: 'annotations-java5'
}

// preBuild.dependsOn ktlint

androidExtensions {
    experimental = true
}

apply plugin: 'com.google.gms.google-services'
